# Copyright 2016 the V8 project authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../gni/isolate.gni")

if (v8_test_isolation_mode != "noop") {
  action("archive_test262") {
    visibility = [ ":*" ]

    script = "archive.py"

    inputs = [
      "list.py",
    ]

    sources = exec_script("list.py", [], "list lines")

    outputs = [
      "$target_gen_dir/test262_archiving.stamp",
    ]

    args = rebase_path(outputs, root_build_dir)
  }
}

v8_isolate_run("test262") {
  deps = [
    ":archive_test262",
    "../..:d8_run",
  ]

  isolate = "test262.isolate"
}
